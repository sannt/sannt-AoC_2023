

метод ВходныеДанные(): Строка

    знч Файл = новый Файл("input.txt")

    исп Поток = Файл.ОткрытьПотокЧтения()
    пер Результат = Поток.ПрочитатьКакСтроку()

    возврат Результат
;


метод Скрипт()

    пер ВходныеДанные = ВходныеДанные()

    пер Сумма = 0

    знч СоответствиеНотацииЧисел = {"one":"1", "two": "2", "three": "3", "four": "4", "five": "5", "six": "6", "seven": "7", "eight": "8", "nine": "9"}

    знч ОбразецПоискаЧиселСловом = новый Образец(Строки.Соединить(СоответствиеНотацииЧисел.Ключи(), "|"))
    знч ОбразецПоискаЧисел = новый Образец(Строки.Соединить([ОбразецПоискаЧиселСловом.ВСтроку(), '\d'], "|"))
    знч ОбразецПоискаРазвернутыхЧисел = новый Образец(Строки.Соединить([ОбразецПоискаЧиселСловом.ВСтроку().Развернуть(), '\d'], "|"))

    для СтрокаДанных из ВходныеДанные.ПолучитьСтроки()

        знч ПервоеСовпадение = ОбразецПоискаЧисел.НайтиСовпадения(Строка = СтрокаДанных, КоличествоСовпадений = 1)
        знч РазвернутаяСтрокаДанных = СтрокаДанных.Развернуть()
        знч ВтороеСовпадение =  ОбразецПоискаРазвернутыхЧисел.НайтиСовпадения(Строка = РазвернутаяСтрокаДанных, КоличествоСовпадений = 1)

        пер КалибровочноеЧисло = ПервоеСовпадение.Первый().Значение() + ВтороеСовпадение.Первый().Значение().Развернуть()

        знч ЗаменительСвязкиНаЧисло = метод(ОчередноеСовпадение: Совпадение) ->
            знч ЗначениеСовпадения = ОчередноеСовпадение.Значение()
            если ЗначениеСовпадения.ТолькоЦифры()
                возврат ЗначениеСовпадения
            иначе
                возврат СоответствиеНотацииЧисел[ЗначениеСовпадения]
            ;
        ;
        КалибровочноеЧисло = КалибровочноеЧисло.Заменить(ОбразецПоискаЧисел, ЗаменительСвязкиНаЧисло)

        Сумма += новый Число(КалибровочноеЧисло)
    ;

    Консоль.Записать(Сумма)

;