
конст КРАСНЫЕ = 12
конст ЗЕЛЕНЫЕ = 13
конст СИНИЕ = 14

метод ВходныеДанные(): Строка

    знч Файл = новый Файл("input.txt")

    исп Поток = Файл.ОткрытьПотокЧтения()
    пер Результат = Поток.ПрочитатьКакСтроку()

    возврат Результат
;

метод Скрипт()

    пер ВходныеДанные = ВходныеДанные()

    пер Сумма = 0

    знч ОбразецПоискаЗначенийКубиков = новый Образец("(?<Число>\\ц+) (?<Цвет_кубика>\\с+)")
    знч ОбразецПоискаИДигры = новый Образец("Game (?<ИД_игры>\\ц+):")

    для СтрокаДанных из ВходныеДанные.ПолучитьСтроки()

        знч Совпадения = ОбразецПоискаЗначенийКубиков.НайтиСовпадения(СтрокаДанных)

        пер ИграВозможна = Истина
        для ОчередноеСовпадение из Совпадения

            знч ЦветКубика = ОчередноеСовпадение.Группа("Цвет_кубика")
            знч ЧислоКубика = новый Число(ОчередноеСовпадение.Группа("Число"))

            если (ЦветКубика == "red" и ЧислоКубика > КРАСНЫЕ)
                или (ЦветКубика == "green" и ЧислоКубика > ЗЕЛЕНЫЕ)
                или (ЦветКубика == "blue" и ЧислоКубика > СИНИЕ)
                ИграВозможна = Ложь
                прервать
            ;
        ;

        если ИграВозможна
            знч СовпаденияИдИгр = ОбразецПоискаИДигры.НайтиСовпадения(Строка = СтрокаДанных, КоличествоСовпадений = 1)
            знч ИДигры = СовпаденияИдИгр.Первый().Группа("ИД_игры")
            Сумма += новый Число(ИДигры)
        ;
    ;

    Консоль.Записать(Сумма)

;