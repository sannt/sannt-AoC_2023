
конст ЦВЕТ_КРАСНЫЙ = "red"
конст ЦВЕТ_ЗЕЛЕНЫЙ = "green"
конст ЦВЕТ_СИНИЙ = "blue"

метод ВходныеДанные(): Строка

    знч Файл = новый Файл("input.txt")

    исп Поток = Файл.ОткрытьПотокЧтения()
    пер Результат = Поток.ПрочитатьКакСтроку()

    возврат Результат
;

метод Скрипт()

    пер ВходныеДанные = ВходныеДанные()

    пер Сумма = 0

    знч ОбразецПоискаЗначенийКубиков = новый Образец("(?<Число>\\ц+) (?<Цвет_кубика>\\с+)")

    для СтрокаДанных из ВходныеДанные.ПолучитьСтроки()

        пер ЗначениеКрасного = 0
        пер ЗначениеЗеленого = 0
        пер ЗначениеСинего = 0

        знч Совпадения = ОбразецПоискаЗначенийКубиков.НайтиСовпадения(СтрокаДанных)

        для ОчередноеСовпадение из Совпадения

            знч ЦветКубика = ОчередноеСовпадение.Группа("Цвет_кубика")
            знч ЧислоКубика = новый Число(ОчередноеСовпадение.Группа("Число"))

            выбор ЦветКубика
            когда ЦВЕТ_КРАСНЫЙ
                ЗначениеКрасного = Макс(ЗначениеКрасного, ЧислоКубика)
            когда ЦВЕТ_ЗЕЛЕНЫЙ
                ЗначениеЗеленого = Макс(ЗначениеЗеленого, ЧислоКубика)
            когда ЦВЕТ_СИНИЙ
                ЗначениеСинего = Макс(ЗначениеСинего, ЧислоКубика)
            ;
        ;

        знч ПроизведениеЗначенийКубиков = ЗначениеКрасного * ЗначениеЗеленого * ЗначениеСинего

        Сумма += ПроизведениеЗначенийКубиков
    ;

    Консоль.Записать(Сумма)

;