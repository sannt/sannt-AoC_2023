
конст ЦВЕТ_КРАСНЫЙ = "red"
конст ЦВЕТ_ЗЕЛЕНЫЙ = "green"
конст ЦВЕТ_СИНИЙ = "blue"

метод ВходныеДанные(): Строка

    знч Файл = новый Файл("input.txt")

    исп Поток = Файл.ОткрытьПотокЧтения()
    пер Результат = Поток.ПрочитатьКакСтроку()

    возврат Результат
;

метод Скрипт()

    пер ВходныеДанные = ВходныеДанные()

    пер Сумма = 0

    знч ОбразецПоискаЗначенийКубиков = новый Образец("(?<Число>\\ц+) (?<Цвет_кубика>\\с+)")

    для СтрокаДанных из ВходныеДанные.ПолучитьСтроки()

        знч Совпадения = ОбразецПоискаЗначенийКубиков.НайтиСовпадения(СтрокаДанных)

        пер ЗначенияКубиков = {ЦВЕТ_КРАСНЫЙ:<Число>[], ЦВЕТ_ЗЕЛЕНЫЙ:<Число>[], ЦВЕТ_СИНИЙ:<Число>[]}

        для ОчередноеСовпадение из Совпадения

            знч ЦветКубика = ОчередноеСовпадение.Группа("Цвет_кубика")
            знч ЧислоКубика = новый Число(ОчередноеСовпадение.Группа("Число"))

            ЗначенияКубиков[ЦветКубика].Добавить(ЧислоКубика)
        ;

        знч ПроизведениеЗначенийКубиков = ЗначенияКубиков[ЦВЕТ_КРАСНЫЙ].Максимум() * ЗначенияКубиков[ЦВЕТ_ЗЕЛЕНЫЙ].Максимум() * ЗначенияКубиков[ЦВЕТ_СИНИЙ].Максимум()

        Сумма += ПроизведениеЗначенийКубиков
    ;

    Консоль.Записать(Сумма)

;