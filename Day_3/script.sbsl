
метод ВходныеДанные(): Строка
    
    знч Файл = новый Файл("input.txt")
    исп Поток = Файл.ОткрытьПотокЧтения()
    возврат Поток.ПрочитатьКакСтроку()

;

метод ПолучитьСтрокиДляПоискаНомеровДетали(ИндексСтроки: Число, ВходныеДанные: Массив<Строка>): Массив<Строка>

    пер Результат = [ВходныеДанные[ИндексСтроки]]

    выбор ИндексСтроки
    когда 0
        Результат.Добавить(ВходныеДанные[ИндексСтроки+1])
    когда ВходныеДанные.Граница()
        Результат.Добавить(ВходныеДанные[ИндексСтроки-1])
    иначе
        Результат.Добавить(ВходныеДанные[ИндексСтроки-1])
        Результат.Добавить(ВходныеДанные[ИндексСтроки+1])
    ;
    возврат Результат

;

метод НайтиНомераДеталиДвигателя(ПозицияСимвола: Число, СтрокиНомеровДетали: Массив<Строка>): Массив<Число>

    пер Результат: Массив<Число>

    для СтрокаДанных из СтрокиНомеровДетали

        знч НайденныеЧисла = '\d+'.НайтиСовпадения(СтрокаДанных)
        для Совпадение из НайденныеЧисла
            знч ПодходящийНомерДетали = Совпадение.Значение()
            знч ДиапазонНомера = новый Диапазон<Число>(
                Совпадение.Индекс()-1,
                Совпадение.Индекс() + ПодходящийНомерДетали.Длина()
            )
            если ДиапазонНомера.Содержит(ПозицияСимвола)
                Результат.Добавить(новый Число(ПодходящийНомерДетали))
            ;
        ;
    ;
    возврат Результат
;

метод Скрипт()

    знч ВходныеДанные = ВходныеДанные().ПолучитьСтроки()

    пер СуммаНомеровДеталей: Число
    пер СуммаПередаточныхЧисел: Число

    для ИндексСтроки = 0 по ВходныеДанные.Граница()

        знч СтрокаДанных = ВходныеДанные.Получить(ИндексСтроки)

        знч НайденныеСимволы = '[^\d\s.]'.НайтиСовпадения(СтрокаДанных)

        для ОчередноеСовпадение из НайденныеСимволы
            знч ПозицияСимвола = ОчередноеСовпадение.Индекс()
            знч СтрокиНомеровДетали = ПолучитьСтрокиДляПоискаНомеровДетали(ИндексСтроки, ВходныеДанные)
            знч НомераДеталей = НайтиНомераДеталиДвигателя(ПозицияСимвола, СтрокиНомеровДетали)
            для НомерДетали из НомераДеталей
                СуммаНомеровДеталей += НомерДетали
            ;
            если ОчередноеСовпадение.Значение() == "*" и НомераДеталей.Размер() > 1
                СуммаПередаточныхЧисел += НомераДеталей.Первый() * НомераДеталей.Последний()
            ;
        ;
    ;
    Консоль.Записать("Сумма номеров деталей: %СуммаНомеровДеталей")
    Консоль.Записать("Сумма передаточных чисел шестеренок: %СуммаПередаточныхЧисел")
;