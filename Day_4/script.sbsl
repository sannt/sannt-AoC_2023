метод ВходныеДанные(): Массив<Строка>
    знч Файл = новый Файл("input.txt")
    исп Поток = Файл.ОткрытьПотокЧтения()
    возврат Поток.ПрочитатьКакСтроку().ПолучитьСтроки()
;

метод КоличествоСовпавшихНомеровБилета(НомераБилета: Строка): Число

    знч РезультатПоиска = '^Card ([\ц\п]+): ([\ц\п]+)\|([\ц\п]+)'.НайтиСовпадения(НомераБилета).Первый()

    знч ВыпавшиеНомера = новый Множество<Строка>(РезультатПоиска.Группа(2).Разделить(" "))
    знч МоиНомера = новый Множество<Строка>(РезультатПоиска.Группа(3).Разделить(" "))

    возврат МоиНомера.Пересечение(ВыпавшиеНомера).Размер()
;

метод Скрипт()

    знч ДанныеБилетов = ВходныеДанные()

    пер РезультатПервойЧасти: Число
    пер РезультатВторойЧасти: Число

    пер КоэфБилетов = новый Соответствие<Число, Число>()

    для ИндексСтроки = 0 по ДанныеБилетов.Граница()

        знч КоличествоСовпавшихНомеров = КоличествоСовпавшихНомеровБилета(ДанныеБилетов[ИндексСтроки])

        // первая часть
        РезультатПервойЧасти += КоличествоСовпавшихНомеров == 0 ? 0 : 2 ** (КоличествоСовпавшихНомеров - 1)

        // вторая часть
        КоэфБилетов[ИндексСтроки] = КоэфБилетов.ПолучитьИлиУмолчание(ИндексСтроки, 0) + 1 //оригинал
        для Счетчик = 1 по КоличествоСовпавшихНомеров
            КоэфБилетов[ИндексСтроки+Счетчик] = КоэфБилетов.ПолучитьИлиУмолчание(ИндексСтроки + Счетчик, 0) + КоэфБилетов[ИндексСтроки]
        ;
    ;
    для Коэффициент из КоэфБилетов
        РезультатВторойЧасти += Коэффициент.Значение
    ;

    Консоль.Записать("Результат первой части: %РезультатПервойЧасти")
    Консоль.Записать("Результат второй части: %РезультатВторойЧасти")

;