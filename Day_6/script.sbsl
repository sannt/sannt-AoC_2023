метод ВходныеДанные(): Массив<Строка>
    знч Файл = новый Файл("input.txt")
    исп Поток = Файл.ОткрытьПотокЧтения()
    возврат Поток.ПрочитатьКакСтроку().ПолучитьСтроки()
;

метод РассчитатьКоличествоВариантовДляПобеды(Время: Число, Дистанция: Число): Число

    знч Дискриминант = Корень(Время ** 2 - 4 * Дистанция)
    знч х1 = (Время - Дискриминант) / 2
    знч х2 = (Время + Дискриминант) / 2

    знч Результат = (х2.Округлить(РежимОкругления.Вверх)) - (х1.ЦелаяЧасть() + 1)
    возврат Результат
;

метод РешениеПервойЧасти()

    знч ВходныеДанные = ВходныеДанные()

    знч ВремяЗаездов = '([\d\s]+)'.НайтиСовпадения(ВходныеДанные.Первый()).Первый().Значение().Разделить(" ")
    знч ДистанцияЗаездов = '([\d\s]+)'.НайтиСовпадения(ВходныеДанные.Последний()).Первый().Значение().Разделить(" ")

    пер ПроизведениеВариаций = 1

    для НомерЗаезда = 0 по ВремяЗаездов.Граница()
        знч Время = новый Число(ВремяЗаездов[НомерЗаезда])
        знч Дистанция = новый Число(ДистанцияЗаездов[НомерЗаезда])
        знч ВариацийПобеды = РассчитатьКоличествоВариантовДляПобеды(Время, Дистанция)
        ПроизведениеВариаций *= ВариацийПобеды
    ;
    Консоль.Записать("Вариантов победы первой части: %ПроизведениеВариаций")
;

метод РешениеВторойЧасти()

    знч ВходныеДанные = ВходныеДанные()

    знч ВремяЗаездов = '([\d\s]+)'.НайтиСовпадения(ВходныеДанные.Первый()).Первый().Значение().Заменить(" ", "")
    знч ДистанцияЗаездов = '([\d\s]+)'.НайтиСовпадения(ВходныеДанные.Последний()).Первый().Значение().Заменить(" ", "")

    знч Время = новый Число(ВремяЗаездов)
    знч Дистанция = новый Число(ДистанцияЗаездов)
    знч ВариацийПобеды = РассчитатьКоличествоВариантовДляПобеды(Время, Дистанция)
    Консоль.Записать("Вариантов победы второй части: %ВариацийПобеды")
;

метод Скрипт()

    РешениеПервойЧасти()
    РешениеВторойЧасти()

;